**FREE
// ===================================================================================================================================
    CTL-OPT DFTACTGRP(*NO) ACTGRP('TIMETRACK') BNDDIR('TSBNDDIR') DEBUG(*YES) OPTION(*NODEBUGIO);
// ===================================================================================================================================
// Program ID       :   TS0202
// Description      :   Display Details (Controller)
// Author           :   Vinayak Mahajan
// Date written     :   September 26, 2016
// Based On Pgm     :   None
// Reviewer         :
// Date Reviewed    :
// ===================================================================================================================================
// Modification History
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Files
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Arrays
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-DS  TSDetailsArray  DIM(999)    QUALIFIED;
            EntryDate       ZONED(8:0);
            StartHH         ZONED(2:0);
            StartMM         ZONED(2:0);
            EndHH           ZONED(2:0);
            EndMM           ZONED(2:0);
            DurationHH      ZONED(2:0);
            DurationMM      ZONED(2:0);
            ActID           ZONED(5:0);
            Activity        CHAR(50);
    END-DS;
// ===================================================================================================================================
// Internal Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-DS  FromDTL_t   BASED(Template) QUALIFIED;
            EntryDate   ZONED(8:0);
            ActID       ZONED(5:0);
            StartHH     ZONED(2:0);
            StartMM     ZONED(2:0);
            EndHH       ZONED(2:0);
            EndMM       ZONED(2:0);
            DrtnHH      ZONED(2:0);
            DrtnMM      ZONED(2:0);
    END-DS;

    DCL-DS  ToDTL_t     BASED(Template) QUALIFIED;
            EntryDate   ZONED(8:0);
            ActID       ZONED(5:0);
            StartHH     ZONED(2:0);
            StartMM     ZONED(2:0);
            EndHH       ZONED(2:0);
            EndMM       ZONED(2:0);
            DrtnHH      ZONED(2:0);
            DrtnMM      ZONED(2:0);
    END-DS;
// ===================================================================================================================================
// External Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// System Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Internal Standalone Fields
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-S   Count           ZONED(5:0);
    DCL-S   pFromDTL        POINTER;
    DCL-S   pToDTL          POINTER;
    DCL-S   pTSDetailsArray POINTER     INZ(%addr(TSDetailsArray));
    DCL-S   TmpDSLRD        ZONED(4:0)  INZ(1);
    DCL-S   UserID          CHAR(10)    INZ(*USER);
// ===================================================================================================================================
// Constants
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Switches
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-S   ExitProgram CHAR(1);
    DCL-S   F3Pressed   CHAR(1);
    DCL-S   F17Pressed  CHAR(1);
    DCL-S   ValidScr01  CHAR(1);
// ===================================================================================================================================
// Prototypes
// -----------------------------------------------------------------------------------------------------------------------------------
    /COPY /QOpenSys/QIBM/UserData/OPS/Orion/serverworkspace/vi/vinayakmahajan/OrionContent/TSP/TS9902PR.sqlrpgle
// ===================================================================================================================================
// Parameters for Various Programs 
// -----------------------------------------------------------------------------------------------------------------------------------

// Parameters for TS0203 - Display Details (View)

    DCL-PR      TS0203          EXTPGM;
    DCL-PARM    Count           ZONED(5:0);
    DCL-PARM    pTSDetailsArray POINTER;
    DCL-PARM    TmpDSLRD        ZONED(4:0);
    DCL-PARM    HideSubset      CHAR(1);
    DCL-PARM    F3Pressed       CHAR(1);
    DCL-PARM    F17Pressed      CHAR(1);
    END-PR;

// Parameters for TS0204 - Subset for Details (Controller)

    DCL-PR      TS0204      EXTPGM;
    DCL-PARM    pFromDTL    POINTER;
    DCL-PARM    pToDTL      POINTER;
    END-PR;

// Parameters for this Program

    DCL-PR      TS0202;
    DCL-PARM    FromDTL     LIKEDS(FromDTL_t);
    DCL-PARM    ToDTL       LIKEDS(ToDTL_t);
    DCL-PARM    HideSubset  CHAR(1);
    END-PR;
// ===================================================================================================================================
// Program Entry Parameters
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-PI      TS0202;
    DCL-PARM    FromDTL     LIKEDS(FromDTL_t);
    DCL-PARM    ToDTL       LIKEDS(ToDTL_t);
    DCL-PARM    HideSubset  CHAR(1);
    END-PI;
// ===================================================================================================================================
//
// -----------------------------------------------------------------------------------------------------------------------------------

    EXSR    InitFirstTime;
    EXSR    InitAlways;
    EXSR    MainProcessing;
    EXSR    ExitAlways;
    EXSR    ExitLastTime;

// ===================================================================================================================================
// Main Processing
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   MainProcessing;

            ExitProgram  =   'N';
        DOW ExitProgram  =   'N';

            EXSR    ProcScr01;

        ENDDO;

    ENDSR;
// ===================================================================================================================================
// Process Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ProcScr01;

            ValidScr01  =   'N';
        DOW ValidScr01  =   'N';

            EXSR    CallView;
            EXSR    VldtScr01;

        ENDDO;

    ENDSR;
// ===================================================================================================================================
// Validate Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   VldtScr01;

        ValidScr01  =   'Y';

        IF  F3Pressed   =   'Y';
            ExitProgram =   'Y';
            F3Pressed   =   'N';
            LEAVESR;
        ENDIF;

        IF  F17Pressed  =   'Y';
            EXSR    ProcSubset;
            ValidScr01  =   'N';
            F17Pressed  =   'N';
        ENDIF;
        
    ENDSR;
// ===================================================================================================================================
// Process Subset
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ProcSubset;

        TS0204(pFromDTL:pToDTL);
        
        EXSR    BuildArray;

    ENDSR;
// ===================================================================================================================================
// Call View
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   CallView;

        TS0203(Count:pTSDetailsArray:TmpDSLRD:HideSubset:F3Pressed:F17Pressed);

    ENDSR;
// ===================================================================================================================================
// Init First Time Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InitFirstTime;

    ENDSR;
// ===================================================================================================================================
// Init Always Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InitAlways;

        pFromDTL =   %addr(FromDTL);
        pToDTL   =   %addr(ToDTL);

        EXSR    BuildArray;

    ENDSR;
// ===================================================================================================================================
// Build Array
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   BuildArray;

        CALLP   TIME_GetTSDetails(UserID:pFromDTL:pToDTL:Count:pTSDetailsArray);

    ENDSR;
// ===================================================================================================================================
// Exit Always Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ExitAlways;

    ENDSR;
// ===================================================================================================================================
// Exit Last Time Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ExitLastTime;

        *InLR   =   *On;
        RETURN;

    ENDSR;
// ===================================================================================================================================