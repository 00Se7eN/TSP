      *=======================================================================*
                                            DSPSIZ(24 80 *DS3)
                                            PRINT
                                            ERRSFL
                                            CF03(03)
                                            CF05(05)
                                            CF06(06)
                                            CF13(13)
      *=======================================================================*
                R TS04031                   SFL
        33                                  SFLNXTCHG
      *-----------------------------------------------------------------------*
                  OPT99          1Y 0B 10  3EDTCDE(Z)
                                            VALUES(0 2 3 4 5)
                  ACTID99        5Y 0O 10  7EDTCDE(Z)
                  ACTDESC99     50A  O 10 14
      *=======================================================================*
                R TS04031C                  SFLCTL(TS04031)
                                            SFLSIZ(0500)
                                            SFLPAG(0011)
                                            OVERLAY
       N32                                  SFLDSP
       N31                                  SFLDSPCTL
        31                                  SFLCLR
        91                                  SFLEND(*MORE)
                  RRN1           4S 0H      SFLRCDNBR(CURSOR *TOP)
      *-----------------------------------------------------------------------*
                                        1  2'TS04031C '
                                            COLOR(BLU)
                                        1 36'Time Sheet'
                                        1 73DATE
                                            EDTCDE(Y)
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        2  2USER
                                            COLOR(BLU)
                                        2 31'Work with Activites'
                                            COLOR(WHT)
                                        2 73TIME
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        4  2'Position to ID. . . . . . .'
                  POSID          5Y 0B  4 30EDTCDE(Z)
        51                                  DSPATR(PC)
                                        4 36'or'
                                            COLOR(WHT)
      *-----------------------------------------------------------------------*
                                        5  2'Position to Description . .'
                  POSDESC       50A  B  5 30
        52                                  DSPATR(PC)
                                            CHECK(LC)
      *-----------------------------------------------------------------------*
                                        7  2'  2=Change         3=Copy         -
                                            4=Delete         5=Display         -
                                                     '
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        9  2'Opt  ActID  Activity Description  -
                                                                               -
                                                     '
                                            COLOR(WHT)
      *=======================================================================*
                R TS04031K
      *-----------------------------------------------------------------------*
                                       22  2'F3=Exit             F5=Refresh    -
                                                  F6=Create        F13=Repeat  -
                                                     '
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                  MESSAGE       78   O 24  2
        50                                  DSPATR(RI)
      *=======================================================================*