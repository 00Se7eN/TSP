**FREE
// ===================================================================================================================================
    CTL-OPT DFTACTGRP(*NO) ACTGRP('TIMETRACK') BNDDIR('TSBNDDIR') DEBUG(*YES) OPTION(*NODEBUGIO);
// ===================================================================================================================================
// Program ID       :   TS0205
// Description      :   Subset for Details (View)
// Author           :   Vinayak Mahajan
// Date written     :   September 26, 2016
// Based On Pgm     :   None
// Reviewer         :
// Date Reviewed    :
// ===================================================================================================================================
// Modification History
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Files
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-F   TS0203DS    WORKSTN INFDS(DSPFINFDS) SFILE(TS02031:RRN1);
// ===================================================================================================================================
// Arrays
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Internal Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-DS  FromDTL     BASED(pFromDTL) QUALIFIED;
            EntryDate   ZONED(8:0);
            ActID       ZONED(5:0);
            StartHH     ZONED(2:0);
            StartMM     ZONED(2:0);
            EndHH       ZONED(2:0);
            EndMM       ZONED(2:0);
            DrtnHH      ZONED(2:0);
            DrtnMM      ZONED(2:0);
    END-DS;

    DCL-DS  ToDTL       BASED(pToDTL)   QUALIFIED;
            EntryDate   ZONED(8:0);
            ActID       ZONED(5:0);
            StartHH     ZONED(2:0);
            StartMM     ZONED(2:0);
            EndHH       ZONED(2:0);
            EndMM       ZONED(2:0);
            DrtnHH      ZONED(2:0);
            DrtnMM      ZONED(2:0);
    END-DS;
// ===================================================================================================================================
// External Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-DS  DSPFINFDS   EXTNAME('DSPFIDS');
    END-DS;
// ===================================================================================================================================
// System Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Internal Standalone Fields
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Constants
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Switches
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Prototypes
// -----------------------------------------------------------------------------------------------------------------------------------
    /COPY /QOpenSys/QIBM/UserData/OPS/Orion/serverworkspace/vi/vinayakmahajan/OrionContent/TSP/TS9902PR.sqlrpgle
// ===================================================================================================================================
// Parameters for Various Programs 
// -----------------------------------------------------------------------------------------------------------------------------------

// Parameters for this Program

    DCL-PR      TS0205;
    DCL-PARM    pFromDTL    POINTER;
    DCL-PARM    pToDTL      POINTER;
    DCL-PARM    F12Pressed  CHAR(1);
    DCL-PARM    Message     CHAR(78);
    DCL-PARM    ErrorID     ZONED(5:0);
    END-PR;
// ===================================================================================================================================
// Program Entry Parameters
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-PI      TS0205;
    DCL-PARM    pFromDTL    POINTER;
    DCL-PARM    pToDTL      POINTER;
    DCL-PARM    F12Pressed  CHAR(1);
    DCL-PARM    Message     CHAR(78);
    DCL-PARM    ErrorID     ZONED(5:0);
    END-PI;
// ===================================================================================================================================
//
// -----------------------------------------------------------------------------------------------------------------------------------

    EXSR    InitScr01;
    EXSR    DispScr01;
    EXSR    AcptScr01;
    EXSR    SetRtrnParms;

    RETURN;

// ===================================================================================================================================
// Initialize Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InitScr01;

        EXSR    ResetScr01Ind;
        EXSR    SetScr01Flds;
        EXSR    SetScr01Cursor;

    ENDSR;
// ===================================================================================================================================
// Reset Screen 01 Indicators
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ResetScr01Ind;

        *In50   =   *Off;

        *In51   =   *Off;
        *In52   =   *Off;
        *In53   =   *Off;
        *In54   =   *Off;
        *In55   =   *Off;

    ENDSR;
// ===================================================================================================================================
// Set Screen 01 Field Values
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   SetScr01Flds;

        FROMDATE    =   FromDTL.EntryDate;
        FROMACTID   =   FromDTL.ActID;
        FROMSTARTH  =   FromDTL.StartHH;
        FROMSTARTM  =   FromDTL.StartMM;
        FROMENDH    =   FromDTL.EndHH;
        FROMENDM    =   FromDTL.EndMM;
        FROMDRTNH   =   FromDTL.DrtnHH;
        FROMDRTNM   =   FromDTL.DrtnMM;

        TODATE      =   ToDTL.EntryDate;
        TOACTID     =   ToDTL.ActID;
        TOSTARTH    =   ToDTL.StartHH;
        TOSTARTM    =   ToDTL.StartMM;
        TOENDH      =   ToDTL.EndHH;
        TOENDM      =   ToDTL.EndMM;
        TODRTNH     =   ToDTL.DrtnHH;
        TODRTNM     =   ToDTL.DrtnMM;

        MESSAGE     =   Message;
        
    ENDSR;
// ===================================================================================================================================
// Set Screen 01 Field Cursor
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   SetScr01Cursor;

        SELECT;
            WHEN    ErrorID =   29;         //From Date > To Date.
                    *In51   =   *On;

            WHEN    ErrorID =   30;         //From Activity ID > To Activity ID.
                    *In52   =   *On;

            WHEN    ErrorID =   31;         //From Start Time > To Start Time.
                    *In53   =   *On;

            WHEN    ErrorID =   32;         //From End Time > To End Time.
                    *In54   =   *On;

            WHEN    ErrorID =   33;         //From Duration > To Duration.
                    *In55   =   *On;
        ENDSL;

    ENDSR;
// ===================================================================================================================================
// Display Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   DispScr01;

        IF  Message <>  *Blanks;
            *In50   =   *On;
        ENDIF;

        WRITE   TS02032;

    ENDSR;
// ===================================================================================================================================
// Accept Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   AcptScr01;

        READ    TS02032;

    ENDSR;
// ===================================================================================================================================
// Set Return Parameters
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   SetRtrnParms;

        IF  *In12               =   *On;
            F12Pressed          =   'Y';
        ELSE;
            // Move Screen Fields to Database Fields
            FromDTL.EntryDate   =   FROMDATE;
            FromDTL.ActID       =   FROMACTID;
            FromDTL.StartHH     =   FROMSTARTH;
            FromDTL.StartMM     =   FROMSTARTM;
            FromDTL.EndHH       =   FROMENDH;
            FromDTL.EndMM       =   FROMENDM;
            FromDTL.DrtnHH      =   FROMDRTNH;
            FromDTL.DrtnMM      =   FROMDRTNM;

            ToDTL.EntryDate     =   TODATE;
            ToDTL.ActID         =   TOACTID;
            ToDTL.StartHH       =   TOSTARTH;
            ToDTL.StartMM       =   TOSTARTM;
            ToDTL.EndHH         =   TOENDH;
            ToDTL.EndMM         =   TOENDM;
            ToDTL.DrtnHH        =   TODRTNH;
            ToDTL.DrtnMM        =   TODRTNM;
        ENDIF;

        IF  ToDTL.EntryDate =   *Zeros;
            ToDTL.EntryDate =   99999999;
        ENDIF;

        IF  ToDTL.ActID     =   *Zeros;
            ToDTL.ActID     =   99999;
        ENDIF;

        IF  ToDTL.StartHH   =   *Zeros;
            ToDTL.StartHH   =   99;
        ENDIF;

        IF  ToDTL.StartMM   =   *Zeros;
            ToDTL.StartMM   =   99;
        ENDIF;

        IF  ToDTL.EndHH     =   *Zeros;
            ToDTL.EndHH     =   99;
        ENDIF;

        IF  ToDTL.EndMM     =   *Zeros;
            ToDTL.EndMM     =   99;
        ENDIF;

        IF  ToDTL.DrtnHH    =   *Zeros;
            ToDTL.DrtnHH    =   99;
        ENDIF;

        IF  ToDTL.DrtnMM    =   *Zeros;
            ToDTL.DrtnMM    =   99;
        ENDIF;

    ENDSR;
// ===================================================================================================================================