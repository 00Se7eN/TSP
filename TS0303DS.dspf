      *=======================================================================*
                                            DSPSIZ(24 80 *DS3)
                                            PRINT
                                            ERRSFL
      *=======================================================================*
                R TS03031                   SFL
        33                                  SFLNXTCHG
      *-----------------------------------------------------------------------*
                  OPT99          1Y 0B  8  3EDTCDE(Z)
                                            VALUES(0 5)
                  DATE99         8Y 0O  8  6
                  DRTNHH99       2  0O  8 16
                                        8 19':'
                  DRTNMM99       2S 0O  8 21
                  ACTID99        5Y 0O  8 25EDTCDE(Z)
                  ACTIVITY99    50A  O  8 31
      *=======================================================================*
                R TS03031C                  SFLCTL(TS03031)
                                            SFLSIZ(0500)
                                            SFLPAG(0013)
                                            OVERLAY
                                            CF03(03)
                                            CF05(05)
                                            CF13(13)
                                            CF17(17)
       N32                                  SFLDSP
       N31                                  SFLDSPCTL
        31                                  SFLCLR
        91                                  SFLEND(*MORE)
                  RRN1           4S 0H      SFLRCDNBR(CURSOR *TOP)
      *-----------------------------------------------------------------------*
                                        1  2'TS03031C '
                                            COLOR(BLU)
                                        1 36'Time Sheet'
                                        1 73DATE
                                            EDTCDE(Y)
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        2  2USER
                                            COLOR(BLU)
                                        2 33'Display Summary'
                                            COLOR(WHT)
                                        2 73TIME
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        4  2'Type options, press Enter.'
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        5  4'5=Display'
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        7  2'Opt Date     Duration  ActID Activ-
                                            ity                                -
                                                      '
                                            COLOR(WHT)
      *=======================================================================*
                R TS03031K
      *-----------------------------------------------------------------------*
                                       22  2'F3=Exit             F5=Refresh    -
                                                  F13=Repeat          F17=Subse-
                                            t        '
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                  MESSAGE       78A  O 24  2
        50                                  DSPATR(RI)
      *=======================================================================*
                R TS03032
                                            CF12(12)
      *-----------------------------------------------------------------------*
                                        1  2'TS03032 '
                                            COLOR(BLU)
                                        1 36'Time Sheet'
                                        1 73DATE
                                            EDTCDE(Y)
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        2  2USER
                                            COLOR(BLU)
                                        2 38'Subset'
                                            COLOR(WHT)
                                        2 73TIME
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        5 19'From'
                                            COLOR(PNK)
                                        5 50'To'
                                            COLOR(PNK)
      *-----------------------------------------------------------------------*
                                        6  2'Date . . . . . .'
                  FROMDATE       8Y 0B  6 19COLOR(WHT)
        51                                  DSPATR(PC)
                  TODATE         8Y 0B  6 50COLOR(WHT)
      *-----------------------------------------------------------------------*
                                        7  2'Activity ID. . .'
                  FROMACTID      5Y 0B  7 19EDTCDE(Z)
                                            COLOR(WHT)
        52                                  DSPATR(PC)
                  TOACTID        5Y 0B  7 50EDTCDE(Z)
                                            COLOR(WHT)
      *-----------------------------------------------------------------------*
                                        8  2'Duration . . . .'
                  FROMDRTNH      2Y 0B  8 19COLOR(WHT)
        53                                  DSPATR(PC)
                  FROMDRTNM      2Y 0B  8 24COLOR(WHT)
                                        8 22':'
                  TODRTNH        2Y 0B  8 50COLOR(WHT)
                  TODRTNM        2Y 0B  8 55COLOR(WHT)
                                        8 53':'
      *-----------------------------------------------------------------------*
                                       22  2'F12=Cancel'
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                  MESSAGE       78A  O 24  2
        50                                  DSPATR(RI)
      *=======================================================================*