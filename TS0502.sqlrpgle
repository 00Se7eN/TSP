**FREE
// ===================================================================================================================================
    CTL-OPT DFTACTGRP(*NO) ACTGRP('TIMETRACK') BNDDIR('TSBNDDIR') DEBUG(*YES) OPTION(*NODEBUGIO);
// ===================================================================================================================================
// Program ID       :   TS0502
// Description      :   Change Settings (Controller)
// Author           :   Vinayak Mahajan
// Date written     :   September 02, 2016
// Based On Pgm     :   None
// Reviewer         :
// Date Reviewed    :
// ===================================================================================================================================
// Modification History
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Arrays
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Internal Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// External Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-DS  UserProfileDS   EXTNAME('TSPPARA') QUALIFIED INZ;
    END-DS;
// ===================================================================================================================================
// System Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Internal Standalone Fields
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-S   DataFormat      ZONED(1:0);
    DCL-S   DateFormat      CHAR(3);
    DCL-S   ErrorID         ZONED(5:0);
    DCL-S   LangCode        CHAR(3)     INZ('ENG');
    DCL-S   Message         CHAR(78);
    DCL-S   pUserProfileDS  POINTER     INZ(%addr(UserProfileDS));
    DCL-S   UserExists      CHAR(1);
    DCL-S   UserID          CHAR(10)    INZ(*USER);
// ===================================================================================================================================
// Constants
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Switches
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-S   ExitProgram CHAR(1);
    DCL-S   F3Pressed   CHAR(1);
    DCL-S   IsValid     CHAR(1);
    DCL-S   ValidScr01  CHAR(1);
// ===================================================================================================================================
// Prototypes
// -----------------------------------------------------------------------------------------------------------------------------------
    /COPY /QOpenSys/QIBM/UserData/OPS/Orion/serverworkspace/vi/vinayakmahajan/OrionContent/TSP/TS9902PR.sqlrpgle
// ===================================================================================================================================
// Parameters for Various Programs 
// -----------------------------------------------------------------------------------------------------------------------------------

// Parameters for TS0503 - Change Settings (View)

    DCL-PR      TS0503          EXTPGM;
    DCL-PARM    pUserProfileDS  POINTER;
    DCL-PARM    F3Pressed       CHAR(1);
    DCL-PARM    Message         CHAR(78);
    DCL-PARM    ErrorID         ZONED(5:0);
    END-PR;
// ===================================================================================================================================
// Program Entry Parameters
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
//
// -----------------------------------------------------------------------------------------------------------------------------------

    EXSR    InitFirstTime;
    EXSR    InitAlways;
    EXSR    MainProcessing;
    EXSR    ExitAlways;
    EXSR    ExitLastTime;

// ===================================================================================================================================
// Main Processing
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   MainProcessing;

        EXSR    GetUserProfile;

            ExitProgram  =   'N';
        DOW ExitProgram  =   'N';

            EXSR    ProcScr01;

        ENDDO;

    ENDSR;
// ===================================================================================================================================
// Process Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ProcScr01;

            ValidScr01  =   'N';
        DOW ValidScr01  =   'N';

            EXSR    CallView;
            EXSR    VldtScr01;

        ENDDO;

    ENDSR;
// ===================================================================================================================================
// Validate Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   VldtScr01;

        Message     =   *Blanks;
        ErrorID     =   *Zeros;
        ValidScr01  =   'Y';

        IF  F3Pressed   =   'Y';
            ExitProgram =   'Y';
            F3Pressed   =   'N';
            LEAVESR;
        ENDIF;

        EXSR    VldtDateEntryFmt;
        IF  IsValid     =   'N';
            ValidScr01  =   'N';
            EXSR    GetErrorMsg;
            LEAVESR;
        ENDIF;

        EXSR    VldtDateDisplayFmt;
        IF  IsValid     =   'N';
            ValidScr01  =   'N';
            EXSR    GetErrorMsg;
            LEAVESR;
        ENDIF;

        EXSR    VldtDataEntryFmt;
        IF  IsValid     =   'N';
            ValidScr01  =   'N';
            EXSR    GetErrorMsg;
            LEAVESR;
        ENDIF;

        EXSR    ProcRequest;
        
    ENDSR;
// ===================================================================================================================================
// Process Request
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ProcRequest;

        IF  UserExists  =   'Y';
            EXSR    UpdateUserProfile;
        ELSE;
            EXSR    InsertUserProfile;
        ENDIF;

    ENDSR;
// ===================================================================================================================================
// Call View
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   CallView;

        TS0503(pUserProfileDS:F3Pressed:Message:ErrorID);

    ENDSR;
// ===================================================================================================================================
// Update User Profile
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   UpdateUserProfile;

        CALLP   TIME_UpdateUserProfile(pUserProfileDS);

    ENDSR;
// ===================================================================================================================================
// Insert User Profile
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InsertUserProfile;

        UserProfileDS.PRKEY1    =   UserID;
        CALLP   TIME_InsertUserProfile(pUserProfileDS);

    ENDSR;
// ===================================================================================================================================
// Validate Date Entry Format
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   VldtDateEntryFmt;

        DateFormat  =   UserProfileDS.PR1001;
        CALLP   TIME_ValidateDateFormat(DateFormat:IsValid:ErrorID);

        SELECT;
            WHEN    ErrorID =   1;          //Date Format is blank.
                    ErrorID =   5;          //Date Entry Format is blank.

            WHEN    ErrorID =   2;          //Date Format is invalid.
                    ErrorID =   6;          //Date Entry Format is invalid.
        ENDSL;

    ENDSR;
// ===================================================================================================================================
// Validate Date Display Format
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   VldtDateDisplayFmt;

        DateFormat  =   UserProfileDS.PR1002;
        CALLP   TIME_ValidateDateFormat(DateFormat:IsValid:ErrorID);

        SELECT;
            WHEN    ErrorID =   1;          //Date Format is blank.
                    ErrorID =   7;          //Date Display Format is blank.

            WHEN    ErrorID =   2;          //Date Format is invalid.
                    ErrorID =   8;          //Date Display Format is invalid.
        ENDSL;

    ENDSR;
// ===================================================================================================================================
// Validate Data Entry Format
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   VldtDataEntryFmt;

        DataFormat  =   UserProfileDS.PR0501;
        CALLP   TIME_ValidateDataFormat(DataFormat:IsValid:ErrorID);

    ENDSR;
// ===================================================================================================================================
// Get User Profile
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   GetUserProfile;

        CALLP TIME_GetUserProfile(UserID:pUserProfileDS:UserExists);

    ENDSR;
// ===================================================================================================================================
// Get Error Message
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   GetErrorMsg;

        CALLP   TIME_GetErrorMessage(ErrorID:LangCode:Message);

    ENDSR;
// ===================================================================================================================================
// Init First Time Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InitFirstTime;

    ENDSR;
// ===================================================================================================================================
// Init Always Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InitAlways;

    ENDSR;
// ===================================================================================================================================
// Exit Always Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ExitAlways;

    ENDSR;
// ===================================================================================================================================
// Exit Last Time Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ExitLastTime;

        *InLR   =   *On;
        RETURN;

    ENDSR;
// ===================================================================================================================================