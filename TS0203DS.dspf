      *=======================================================================*
                                            DSPSIZ(27 132 *DS4                 -
                                                   24 80 *DS3)
                                            PRINT
                                            ERRSFL
                                            CF03(03)
      *=======================================================================*
                R TS02031                   SFL
        33                                  SFLNXTCHG
      *-----------------------------------------------------------------------*
                  DATE99         8S 0O  5  2
                  STRHH99        2S 0O  5 12
                  STRMM99        2S 0O  5 17
                                        5 15':'
                  ENDHH99        2S 0O  5 21
                  ENDMM99        2S 0O  5 26
                                        5 24':'
                  DRTNHH99       2S 0O  5 30
                  DRTNMM99       2S 0O  5 35
                                        5 33':'
                  ACTID99        5Y 0O  5 39EDTCDE(Z)
                  ACTIVITY99    50A  O  5 46
      *=======================================================================*
                R TS02031C                  SFLCTL(TS02031)
        *DS4                                SFLSIZ(0500)
        *DS3                                SFLSIZ(0500)
        *DS4                                SFLPAG(0019)
        *DS3                                SFLPAG(0009)
                                            OVERLAY
                                            CF17(17)
       N32                                  SFLDSP
       N31                                  SFLDSPCTL
        31                                  SFLCLR
        91                                  SFLEND(*MORE)
                  RRN1           4S 0H      SFLRCDNBR(CURSOR *TOP)
      *-----------------------------------------------------------------------*
                                        1  2'TS02031C '
                                            COLOR(BLU)
                                        1 62'Time Sheet'
                                        1125DATE
                                            EDTCDE(Y)
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        2  2USER
                                            COLOR(BLU)
                                        2 59'Display Details'
                                            COLOR(WHT)
                                        2125TIME
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        4  2'Date       Start     End   Duratio-
                                            n  ActID  Activity'
                                            COLOR(WHT)
      *=======================================================================*
                R TS02031K
                                            DSPMOD(*DS4)
      *-----------------------------------------------------------------------*
                                       25  2'F3=Exit'
                                            COLOR(BLU)
                                       25 22'F17=Subset'
                                            COLOR(BLU)
        95                                  DSPATR(ND)
      *-----------------------------------------------------------------------*
                  MESSAGE      130A  O 27  2
        50                                  DSPATR(RI)
      *=======================================================================*
                R TS02032
                                            DSPMOD(*DS3)
      *-----------------------------------------------------------------------*
                                        1  2'TS02032 '
                                            COLOR(BLU)
                                        1 36'Time Sheet'
                                        1 73DATE
                                            EDTCDE(Y)
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        2  2USER
                                            COLOR(BLU)
                                        2 38'Subset'
                                            COLOR(WHT)
                                        2 73TIME
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                                        5 19'From'
                                            COLOR(PNK)
                                        5 50'To'
                                            COLOR(PNK)
      *-----------------------------------------------------------------------*
                                        6  2'Date . . . . . .'
                  FROMDATE       8Y 0B  6 19COLOR(WHT)
        51                                  DSPATR(PC)
                  TODATE         8Y 0B  6 50COLOR(WHT)
      *-----------------------------------------------------------------------*
                                        7  2'Activity ID. . .'
                  FROMACTID      5Y 0B  7 19EDTCDE(Z)
                                            COLOR(WHT)
        52                                  DSPATR(PC)
                  TOACTID        5Y 0B  7 50EDTCDE(Z)
                                            COLOR(WHT)
      *-----------------------------------------------------------------------*
                                        8  2'Start Time . . .'
                  FROMSTARTH     2Y 0B  8 19COLOR(WHT)
        53                                  DSPATR(PC)
                  FROMSTARTM     2Y 0B  8 24COLOR(WHT)
                                        8 22':'
                  TOSTARTH       2Y 0B  8 50COLOR(WHT)
                  TOSTARTM       2Y 0B  8 55COLOR(WHT)
                                        8 53':'
      *-----------------------------------------------------------------------*
                                        9  2'End Time . . . .'
                  FROMENDH       2Y 0B  9 19COLOR(WHT)
        54                                  DSPATR(PC)
                  FROMENDM       2Y 0B  9 24COLOR(WHT)
                                        9 22':'
                  TOENDH         2Y 0B  9 50COLOR(WHT)
                  TOENDM         2Y 0B  9 55COLOR(WHT)
                                        9 53':'
      *-----------------------------------------------------------------------*
                                       10  2'Duration . . . .'
                  FROMDRTNH      2Y 0B 10 19COLOR(WHT)
        55                                  DSPATR(PC)
                  FROMDRTNM      2Y 0B 10 24COLOR(WHT)
                                       10 22':'
                  TODRTNH        2Y 0B 10 50COLOR(WHT)
                  TODRTNM        2Y 0B 10 55COLOR(WHT)
                                       10 53':'
      *-----------------------------------------------------------------------*
                                       22  2'F3=Exit'
                                            COLOR(BLU)
      *-----------------------------------------------------------------------*
                  MESSAGE       78A  O 24  2
        50                                  DSPATR(RI)
      *=======================================================================*