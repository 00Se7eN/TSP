**FREE
// ===================================================================================================================================
    CTL-OPT DFTACTGRP(*NO) ACTGRP('TIMETRACK') BNDDIR('TSBNDDIR') DEBUG(*YES) OPTION(*NODEBUGIO);
// ===================================================================================================================================
// Program ID       :   TS0304
// Description      :   Subset for Summary (Controller)
// Author           :   Vinayak Mahajan
// Date written     :   September 27, 2016
// Based On Pgm     :   None
// Reviewer         :
// Date Reviewed    :
// ===================================================================================================================================
// Modification History
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Files
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Arrays
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Internal Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-DS  FromSUM     BASED(pFromSUM) QUALIFIED;
            EntryDate   ZONED(8:0);
            ActID       ZONED(5:0);
            DrtnHH      ZONED(2:0);
            DrtnMM      ZONED(2:0);
    END-DS;

    DCL-DS  ToSUM       BASED(pToSUM)   QUALIFIED;
            EntryDate   ZONED(8:0);
            ActID       ZONED(5:0);
            DrtnHH      ZONED(2:0);
            DrtnMM      ZONED(2:0);
    END-DS;
// ===================================================================================================================================
// External Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// System Data Structures
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Internal Standalone Fields
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-S   ErrorID         ZONED(5:0);
    DCL-S   LangCode        CHAR(3)     INZ('ENG');
    DCL-S   Message         CHAR(78);
// ===================================================================================================================================
// Constants
// -----------------------------------------------------------------------------------------------------------------------------------
// ===================================================================================================================================
// Switches
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-S   ExitProgram     CHAR(1);
    DCL-S   F12Pressed      CHAR(1);
    DCL-S   ValidScr01      CHAR(1);
// ===================================================================================================================================
// Prototypes
// -----------------------------------------------------------------------------------------------------------------------------------
    /COPY /QOpenSys/QIBM/UserData/OPS/Orion/serverworkspace/vi/vinayakmahajan/OrionContent/TSP/TS9902PR.sqlrpgle
// ===================================================================================================================================
// Parameters for Various Programs
// -----------------------------------------------------------------------------------------------------------------------------------

// Parameters for TS0304 - Display Summary (View)

    DCL-PR      TS0305      EXTPGM;
    DCL-PARM    pFromSUM    POINTER;
    DCL-PARM    pToSUM      POINTER;
    DCL-PARM    F12Pressed  CHAR(1);
    DCL-PARM    Message     CHAR(78);
    DCL-PARM    ErrorID     ZONED(5:0);
    END-PR;

// Parameters for this Program

    DCL-PR      TS0304;
    DCL-PARM    pFromSUM    POINTER;
    DCL-PARM    pToSUM      POINTER;
    END-PR;
// ===================================================================================================================================
// Program Entry Parameters
// -----------------------------------------------------------------------------------------------------------------------------------
    DCL-PI      TS0304;
    DCL-PARM    pFromSUM    POINTER;
    DCL-PARM    pToSUM      POINTER;
    END-PI;
// ===================================================================================================================================
//
// -----------------------------------------------------------------------------------------------------------------------------------

    EXSR    InitFirstTime;
    EXSR    InitAlways;
    EXSR    MainProcessing;
    EXSR    ExitAlways;
    EXSR    ExitLastTime;

// ===================================================================================================================================
// Main Processing
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   MainProcessing;

            ExitProgram  =   'N';
        DOW ExitProgram  =   'N';

            EXSR    ProcScr01;

        ENDDO;

    ENDSR;
// ===================================================================================================================================
// Process Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ProcScr01;

            ValidScr01  =   'N';
        DOW ValidScr01  =   'N';

            EXSR    CallView;
            EXSR    VldtScr01;

        ENDDO;

    ENDSR;
// ===================================================================================================================================
// Validate Screen 01
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   VldtScr01;

        Message         =   *Blanks;
        ErrorID         =   *Zeros;
        ValidScr01      =   'Y';

        IF  F12Pressed  =   'Y';
            ExitProgram =   'Y';
            F12Pressed  =   'N';
            LEAVESR;
        ENDIF;

        SELECT;
            WHEN    FromSUM.EntryDate   >   ToSUM.EntryDate;
                    ValidScr01          =   'N';
                    ErrorID             =   29;         //From Date > To Date.
                    EXSR    GetErrorMsg;

            WHEN    FromSUM.ActID       >   ToSUM.ActID;
                    ValidScr01          =   'N';
                    ErrorID             =   30;         //From Activity ID > To Activity ID.
                    EXSR    GetErrorMsg;

            WHEN    FromSUM.DrtnHH      >   ToSUM.DrtnHH;
                    ValidScr01          =   'N';
                    ErrorID             =   33;         //From Duration > To Duration.
                    EXSR    GetErrorMsg;

            WHEN    FromSUM.DrtnHH      =   ToSUM.DrtnHH    AND
                    FromSUM.DrtnMM      >   ToSUM.DrtnMM;
                    ValidScr01          =   'N';
                    ErrorID             =   33;         //From Duration > To Duration.
                    EXSR    GetErrorMsg;
        ENDSL;

        IF  ValidScr01  =   'Y';
            ExitProgram =   'Y';
        ENDIF;

    ENDSR;
// ===================================================================================================================================
// Call View
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   CallView;

        CALLP   TS0305(pFromSUM:pToSUM:F12Pressed:Message:ErrorID);

    ENDSR;
// ===================================================================================================================================
// Get Error Message
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   GetErrorMsg;

        CALLP   TIME_GetErrorMessage(ErrorID:LangCode:Message);

    ENDSR;
// ===================================================================================================================================
// Init First Time Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InitFirstTime;

    ENDSR;
// ===================================================================================================================================
// Init Always Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   InitAlways;

    ENDSR;
// ===================================================================================================================================
// Exit Always Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ExitAlways;

    ENDSR;
// ===================================================================================================================================
// Exit Last Time Subroutine
// -----------------------------------------------------------------------------------------------------------------------------------
    BEGSR   ExitLastTime;

        *InLR   =   *On;
        RETURN;

    ENDSR;
// ===================================================================================================================================